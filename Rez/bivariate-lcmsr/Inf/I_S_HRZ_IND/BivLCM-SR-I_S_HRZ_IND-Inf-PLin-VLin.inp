
title: PxVx Bivariate LCM-SR - Inf Linear I_S with Linear HRZ_IND;
data: file = /Users/johnflournoy/code/lnt_pxvx/Data/LT_informants3.txt;

variable:
names =
subjid
Sample
aGENDER
aAGE
aethnic1
aethnic2
aedu_mom
aedu_dad
aEDUCATN
bEDUCATN
cEDUCATN
aEMPLOYD
bEMPLOYD
cEMPLOYD
dEMPLOYD
aINCOME
bINCOME
cINCOME
dINCOME
aS_SCALE
bS_SCALE
cS_SCALE
dS_SCALE
aD_SCALE
bD_SCALE
cD_SCALE
dD_SCALE
aS_COMP
bS_COMP
cS_COMP
dS_COMP
aD_COMP
bD_COMP
cD_COMP
dD_COMP
aBFI_A
aBFI_A6
aBFI_C
aBFI_E
aBFI_HP
aBFI_N
aBFI_O
bBFI_A
bBFI_A6
bBFI_C
bBFI_E
bBFI_HP
bBFI_N
bBFI_O
cBFI_A
cBFI_A6
cBFI_C
cBFI_E
cBFI_HP
cBFI_N
cBFI_O
dBFI_A
dBFI_A6
dBFI_C
dBFI_E
dBFI_HP
dBFI_N
dBFI_O
aBFA_AC
aBFA_AP
aBFA_CI
aBFA_CO
aBFA_EA
aBFA_EE
aBFA_N9
aBFA_NV
aBFA_NW
aBFA_OI
aBFA_OO
aBFA_PS
aBFA_MT
bBFA_AC
bBFA_AP
bBFA_CI
bBFA_CO
bBFA_EA
bBFA_EE
bBFA_N9
bBFA_NV
bBFA_NW
bBFA_OI
bBFA_OO
bBFA_MT
bBFA_PS
cBFA_AC
cBFA_AP
cBFA_CI
cBFA_CO
cBFA_EA
cBFA_EE
cBFA_N9
cBFA_NV
cBFA_NW
cBFA_OI
cBFA_OO
cBFA_MT
cBFA_PS
dBFA_AC
dBFA_AP
dBFA_CI
dBFA_CO
dBFA_EA
dBFA_EE
dBFA_N9
dBFA_NV
dBFA_NW
dBFA_OI
dBFA_OO
dBFA_MT
dBFA_PS
aUSI
bUSI
cUSI
dUSI
aHRZ_IND
bHRZ_IND
cHRZ_IND
dHRZ_IND
aVRT_IND
bVRT_IND
cVRT_IND
dVRT_IND
aHRZ_COL
bHRZ_COL
cHRZ_COL
dHRZ_COL
aVRT_COL
bVRT_COL
cVRT_COL
dVRT_COL
aCOLLCTV
bCOLLCTV
cCOLLCTV
dCOLLCTV
aMVS
bMVS
cMVS
dMVS
aST
bST
cST
dST
aSD
bSD
cSD
dSD
aMVS_mc
bMVS_mc
cMVS_mc
dMVS_mc
aST_mc
bST_mc
cST_mc
dST_mc
aSD_mc
bSD_mc
cSD_mc
dSD_mc
bMEANING
cMEANING
dMEANING
bMEAN_L
cMEAN_L
dMEAN_L
bMEANNGc
cMEANNGc
dMEANNGc
bMEANGlc
cMEANGlc
dMEANGlc
bCLASSIC
cCLASSIC
dCLASSIC
bCLASSCc
cCLASSCc
dCLASSCc
bAGR_VAL
cAGR_VAL
dAGR_VAL
bOPN_VAL
cOPN_VAL
dOPN_VAL
bAGR_VLc
cAGR_VLc
dAGR_VLc
bOPNVALc
cOPNVALc
dOPNVALc
aMV_pomp
bMV_pomp
cMV_pomp
dMV_pomp
aST_pomp
bST_pomp
cST_pomp
dST_pomp
aSD_pomp
bSD_pomp
cSD_pomp
dSD_pomp
agoal_eco
bgoal_eco
cgoal_eco
dgoal_eco
aAspfinc
bAspfinc
cAspfinc
dAspfinc
abfi_hp8
bbfi_hp8
cbfi_hp8
dbfi_hp8
aP_S_BFI
aP_D_BFI
aECgoalc
bECgoalC
cECgoalC
dECgoalC
aaspfin
baspfin
caspfin
daspfin
aasp_mn2
basp_mn2
casp_mn2
dasp_mn2
aas12mc2
aas13mc2
aas14mc2
aas15mc2
aas16mc2
bas12mc2
bas13mc2
bas14mc2
bas15mc2
bas16mc2
cas12mc2
cas13mc2
cas14mc2
cas15mc2
cas16mc2
das12mc2
das13mc2
das14mc2
das15mc2
das16mc2
aas_f_c2
bas_f_c2
cas_f_c2
das_f_c2
aI_E
aI_A
aI_C
aI_N
aI_O
aI_S
aI_D
bI_E
bI_A
bI_C
bI_N
bI_O
bI_S
bI_D
cI_E
cI_A
cI_C
cI_N
cI_O
cI_S
cI_D
dI_E
dI_A
dI_C
dI_N
dI_O
dI_S
dI_D
T1_any_I
T2_any_I
T3_any_I
T4_any_I
aI_H
bI_H
cI_H
dI_H
aMVI_POMP
bMVI_POMP
cMVI_POMP
dMVI_POMP;



USEVARIABLES =
aI_S
bI_S
cI_S
dI_S
aHRZ_IND
bHRZ_IND
cHRZ_IND
dHRZ_IND
a1
a2
a3
a4
;

missing = all (-9999);

USEOBSERVATIONS are Sample == 1; ! National Sample

tscores = a1 a2 a3 a4;

DEFINE:
a1 = aAGE + 0 - 36;
a2 = aAGE + 1 - 36;
a3 = aAGE + 2 - 36;
a4 = aAGE + 3 - 36;

ANALYSIS:
TYPE=RANDOM;
PROCESSORS = 4; 
model=nocovariances; ! supresses default estimation of certain covs in model;

MODEL:

i_I_S s_I_S | aI_S bI_S cI_S dI_S AT a1 a2 a3 a4;
i_HRZ_IND s_HRZ_IND | aHRZ_IND bHRZ_IND cHRZ_IND dHRZ_IND AT a1 a2 a3 a4;

! freeing factor covs because of "nocovariances"
s_I_S WITH i_I_S s_HRZ_IND i_HRZ_IND; 
i_I_S WITH s_HRZ_IND i_HRZ_IND;
s_HRZ_IND WITH i_HRZ_IND;

p1 BY aI_S@1; ! Residual "factors";
p2 BY bI_S@1;
p3 BY cI_S@1;
p4 BY dI_S@1;

v1 BY aHRZ_IND@1; ! Residual "factors";
v2 BY bHRZ_IND@1;
v3 BY cHRZ_IND@1;
v4 BY dHRZ_IND@1;

!residual variances on observed/manifest vars set to 0
aI_S@0;
bI_S@0;
cI_S@0;
dI_S@0;

!residual variances on observed/manifest vars set to 0
aHRZ_IND@0;
bHRZ_IND@0;
cHRZ_IND@0;
dHRZ_IND@0;

! means of observed/manifest vars set to 0
[aI_S@0];
[bI_S@0];
[cI_S@0];
[dI_S@0];

! means of observed/manifest vars set to 0
[aHRZ_IND@0];
[bHRZ_IND@0];
[cHRZ_IND@0];
[dHRZ_IND@0];

[p1@0]; ! means of residual "factors" set to 0
[p2@0];
[p3@0];
[p4@0];

[v1@0]; ! means of residual "factors" set to 0
[v2@0];
[v3@0];
[v4@0];

p4 on p3 (a); ! Autoregressive paths constrained to equality
p3 on p2 (a);
p2 on p1 (a);

v4 on v3 (b); ! Autoregressive paths constrained to equality
v3 on v2 (b);
v2 on v1 (b);

p4 on v3 (c); ! Cross-lagged paths constrained to equality
p3 on v2 (c);
p2 on v1 (c);

v4 on p3 (d); ! Cross-lagged paths constrained to equality
v3 on p2 (d);
v2 on p1 (d);

p1 with v1 (e); ! Within-wave correlations constrained to equality
p2 with v2 (e);
p3 with v3 (e);
p4 with v4 (e);

p2 p3 p4 (f); ! estimate variances of residual "factors"
v2 v3 v4 (g); ! estimate variances of residual "factors"

OUTPUT: SAMPSTAT PATTERNS;
